Version=5.9
AppType=JavaFX
NumberOfModules=0
Build1=Default,b4j.example
NumberOfFiles=1
File1=main.bjl
NumberOfLibraries=4
Library1=jcore
Library2=jfx
Library3=apachestringutilsb4j
Library4=jreflection
@EndOfDesignText@
#Region Project Attributes 
	#MainFormWidth: 600
	#MainFormHeight: 600 
#End Region

Sub Process_Globals
	Private fx As JFX
	Private MainForm As Form
	Private Button1 As Button
	Private TextField1 As TextField
	Private TextField2 As TextField
	Dim a(,) As Int
	Private WebView1 As WebView
	Private Button2 As Button
	Private WebView2 As WebView
End Sub

Sub AppStart (Form1 As Form, Args() As String)
	MainForm = Form1
	MainForm.RootPane.LoadLayout("main") 'Load the layout file.
	MainForm.Show
End Sub

'Return true to allow the default exceptions handler to handle the uncaught exception.
Sub Application_Error (Error As Exception, StackTrace As String) As Boolean
	Return True
End Sub

Sub Button1_MouseClicked (EventData As MouseEvent)
	fx.Msgbox(MainForm,similarity(editDistance),"")
End Sub

Sub editDistance As Int
	Dim str1 As String
	Dim str2 As String
	If TextField1.Text.Length<TextField2.Text.Length Then
		Dim tmp As String
		tmp=TextField1.Text
		TextField1.Text=TextField2.Text
		TextField2.Text=tmp
	End If
	str1=TextField1.Text
	str2=TextField2.Text
	
	Dim maxLength As Int
	maxLength=Max(str1.Length,str2.Length)
	If maxLength=0 Then
		fx.Msgbox(MainForm,"","")
	End If

	
	
	'int
	Dim a(str1.Length+1,str2.Length+1) As Int 'str1是放在上面的，影响列
    a(0,0)=0
	
	For i=0 To str1.Length-1
		a(i+1,0)=a(i,0)+1
	Next

	For i=0 To str2.Length-1
		a(0,i+1)=a(0,i)+1
	Next
	
	
	'dp
	Dim temp As Int
	For j=1 To str2.Length
	    For i=1 To str1.Length
			Log(i&" "&j)
			If str1.CharAt(i-1)<>str2.CharAt(j-1) Then
				temp=1
			Else
				temp=0
			End If
			a(i,j)=Min(a(i-1,j-1)+temp,Min(a(i,j-1)+1,a(i-1,j)+1))
		Next
	Next
	
	Dim content As String
	For j=0 To str2.Length
		Dim row As String
		For i=0 To str1.Length
			If i=0 Then
				row=a(i,j)
			Else
				row=row&","&a(i,j)
			End If
		Next
		Log(row)
		content=content&row&CRLF
	Next
	WebView1.LoadHtml(content.Replace(CRLF,"<br />"))
	File.WriteString(File.DirApp,"out.csv",content)
	
	Return a(str1.Length,str2.Length)
	
End Sub

Sub similarity(distance As Int) As Double
	Dim result As Double
	Log(distance)
	Log(Max(TextField1.Text.Length,TextField2.Text.Length))
	result=1-distance/Max(TextField1.Text.Length,TextField2.Text.Length)
	Log(result)
	Return result
End Sub



Sub Button2_MouseClicked (EventData As MouseEvent)
	revealOperation
End Sub

Sub revealOperation
	Dim str1 As String
	Dim str2 As String
	str1=TextField1.Text
	str2=TextField2.Text
	Dim maxLength As Int
	maxLength=Max(str1.Length,str2.Length)
	Dim list1 As List
	list1.Initialize
	
	Dim x,y As Int
	x=str1.Length
	y=str2.Length
	For i=maxLength To 0 Step -1
		Dim map1 As Map
		map1=getWayAndPos(x,y)
		list1.Add(map1)
		x=map1.Get("x")
		y=map1.Get("y")
	Next
	Log(list1)
	interpret(list1)
End Sub

Sub getWayAndPos(x As Int,y As Int) As Map
	Dim left,diagonal,up As Int
	Dim way As String
	Dim map1 As Map
	map1.Initialize
	If x-1=-1 And y-1<>-1 Then
		way="up"
		y=y-1
		map1.Put("way",way)
		map1.Put("x",x)
		map1.Put("y",y)
		Return map1
	End If
	If y-1=-1 And x-1<>-1 Then
		way="left"
		x=x-1
		map1.Put("way",way)
		map1.Put("x",x)
		map1.Put("y",y)
		Return map1
	End If
	If y-1=-1 And x-1=-1 Then
		way="end"
		map1.Put("way",way)
		map1.Put("x",x)
		map1.Put("y",y)
		Return map1
	End If
	left=a(x-1,y)
	diagonal=a(x-1,y-1)
	up=a(x,y-1)
	'优先级按照左上角、上边、左边的顺序
	If left<up Then
		If left<diagonal Then
			way="left"
		End If
	End If
	If up<left Then
		If up<diagonal Then
			way="up"
		End If
	End If
	If left=up Then
		way="up"
	End If
	If diagonal<=left And diagonal<=up Then
		way="diagonal"
	End If
	Select way
		Case "up"
			y=y-1
		Case "left"
			x=x-1
		Case "diagonal"
			x=x-1
			y=y-1
	End Select

	map1.Put("way",way)
	map1.Put("x",x)
	map1.Put("y",y)
	Return map1
End Sub

Sub interpret(list1 As List)
	Dim add,del,substitute,diff As String
	Dim diffList,diffPosList,addList,addPosList As List
	diffList.Initialize
	addList.Initialize
	addPosList.Initialize
	diffPosList.Initialize
	For Each map1 As Map In list1
		Dim text As String
		If map1.Get("way")="up" Then
			Log("Add "&TextField2.Text.CharAt(map1.Get("y")))
			add=add&TextField2.Text.CharAt(map1.Get("y"))
			
			addPosList.InsertAt(0,map1.Get("y"))
			text=TextField2.Text.CharAt(map1.Get("y"))
			addList.InsertAt(0,text)
		Else if map1.Get("way")="left" Then
			Log("Del "&TextField1.Text.CharAt(map1.Get("x")))
			del=del&TextField1.Text.CharAt(map1.Get("x"))
			diff=diff&TextField1.Text.CharAt(map1.Get("x"))
			text=TextField1.Text.CharAt(map1.Get("x"))
			diffList.InsertAt(0,text)
			diffPosList.InsertAt(0,map1.Get("x"))
		Else if map1.Get("way")="diagonal" Then
			If TextField1.Text.CharAt(map1.Get("x"))<>TextField2.Text.CharAt(map1.Get("y")) Then
				Log("substitute "&TextField1.Text.CharAt(map1.Get("x")))
				substitute=substitute&TextField1.Text.CharAt(map1.Get("x"))
				diff=diff&TextField1.Text.CharAt(map1.Get("x"))
				text=TextField1.Text.CharAt(map1.Get("x"))
				diffList.InsertAt(0,text)
				diffPosList.InsertAt(0,map1.Get("x"))
				'Log("Switch "&TextField2.Text.CharAt(map1.Get("y")))
			End If
		End If
	Next

	Dim su As ApacheSU
	add=su.Reverse(add)
	diff=su.Reverse(diff) 
	del=su.Reverse(del)
	substitute=su.Reverse(substitute)
	'Log(add&del&substitute)
	Log(diff)
	WebView2.LoadHtml(genHtmlResult(addList,diffList,addPosList,diffPosList))
End Sub

Sub genHtmlResult(addList As List,diffList As List,addPosList As List,diffPosList As List) As String
	Dim content As String
	For i=0 To TextField1.Text.Length-1
		Dim text As String
		text=TextField1.Text.CharAt(i)
		If diffList.IndexOf(text)<>-1 And diffList.Size<>0 Then
			If diffPosList.Get(diffList.IndexOf(text))=i Then
				diffPosList.RemoveAt(diffList.IndexOf(text))
				diffList.RemoveAt(diffList.IndexOf(text))
				text="<font color="&Chr(34)&"red"&Chr(34)&">"&text&"</font>"
			End If
		End If
		content=content&text
	Next
	content="<p>"&content&"</p><p>"
	
	For i=0 To TextField2.Text.Length-1
		Dim text As String
		text=TextField2.Text.CharAt(i)
		If addList.IndexOf(text)<>-1 And addList.Size<>0 Then
			
			If addPosList.Get(addList.IndexOf(text))=i Then
				addPosList.RemoveAt(addList.IndexOf(text))
				addList.RemoveAt(addList.IndexOf(text))
				text="<font color="&Chr(34)&"green"&Chr(34)&">"&text&"</font>"
			End If
		End If
		content=content&text
	Next
	content=content&"</p>"
	content="<!DOCTYPE HTML><html><body>"&content&"</body></html>"
	Log(content)
    Return content
End Sub

Sub genHtmlResult_old(add As String,del As String,substitute As String,diff As String) As String
	Dim content As String
	content="<p>"&TextField1.Text&"</p>"&TextField2.Text&"</p>"
	Dim su As ApacheSU
	Log("d"&su.Difference(TextField2.Text,TextField1.Text))
	
	If add<>"" Then
		'for each item as string in su.SplitWithSeparator
		content=content.Replace(add,"<font color="&Chr(34)&"green"&Chr(34)&">"&add&"</font>")
	End If
	If del<>"" Then
		'content=content.Replace(del,"<del>"&del&"</del>")
	End If
	If substitute<>"" Then
		'content=content.Replace(substitute,"<font color="&Chr(34)&"red"&Chr(34)&">"&substitute&"</font>")
	End If
	'替换和删除操作都是长的文本有，短的没有的，可以统一起来显示差异。
	Dim list1 As List
	list1.Initialize
	For i=0 To diff.Length-1
		list1.Add(diff.CharAt(i))
	Next
	Log(list1)
	If diff<>"" Then
		For Each item As String In su.SplitWithSeparator(diff," ")
			content=content.Replace(item,"<font color="&Chr(34)&"red"&Chr(34)&">"&item&"</font>")
		Next
		'content=content.Replace(diff,"<font color="&Chr(34)&"red"&Chr(34)&">"&diff&"</font>")
	End If
    
	content="<!DOCTYPE HTML><html><body>"&content&"</body></html>"
	Log(content)
	Return content
End Sub