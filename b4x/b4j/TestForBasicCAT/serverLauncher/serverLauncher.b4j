AppType=JavaFX
Build1=Default,b4j.example
File1=serverLanucher.bjl
FileGroup1=Default Group
Group=Default Group
Library1=jcore
Library2=jfx
Library3=jshell
NumberOfFiles=1
NumberOfLibraries=3
NumberOfModules=0
Version=6.51
@EndOfDesignText@
#Region Project Attributes 
	#MainFormWidth: 600
	#MainFormHeight: 600 
#End Region

Sub Process_Globals
	Private fx As JFX
	Private MainForm As Form
	Private languageToolPathTextField As TextField
	Private stanfordPathTextField As TextField
	Private languageToolShell As Shell
	Private corenlpShell As Shell
	Private launchCorenlpButton As Button
	Private launchLanguageToolButton As Button
	Private pathSetting As Map
End Sub

Sub AppStart (Form1 As Form, Args() As String)
	MainForm = Form1
	MainForm.RootPane.LoadLayout("serverLanucher") 'Load the layout file.
	MainForm.Show
	testJavaPath
	If File.Exists(File.DirData("BasicCAT"),"serverpath") Then
		pathSetting=File.ReadMap(File.DirData("BasicCAT"),"serverpath")
		If pathSetting.ContainsKey("languagetool") Then
			languageToolPathTextField.Text=pathSetting.Get("languagetool")
		End If
		If pathSetting.ContainsKey("corenlp") Then
			stanfordPathTextField.Text=pathSetting.Get("corenlp")
		End If
	Else
		pathSetting.Initialize
	End If
End Sub


Sub MainForm_CloseRequest (EventData As Event)
	Dim result As Int
	result=fx.Msgbox2(MainForm,"Servers will be closed, continue?","","Yes","Cancel","",fx.MSGBOX_CONFIRMATION)
	Select result
		'yes -1, no -2, cancel -3
		Case -3
			EventData.Consume
	End Select
	Try
	    corenlpShell.KillProcess
	Catch
		Log(LastException)
	End Try
	Try
		languageToolShell.KillProcess
	Catch
		Log(LastException)
	End Try
End Sub

'Return true to allow the default exceptions handler to handle the uncaught exception.
Sub Application_Error (Error As Exception, StackTrace As String) As Boolean
	Return True
End Sub

Sub testJavaPath As ResumableSub
	Dim sh1 As Shell
	sh1.Initialize("sh1","java",Array As String("-version"))
	sh1.Run(5000)
	wait for sh1_ProcessCompleted (Success As Boolean, ExitCode As Int, StdOut As String, StdErr As String)
	If Success And ExitCode = 0 Then
		Log("Success")
		Return True
	Else
		Log("Error: " & StdErr)
		fx.Msgbox(MainForm,"java is not installed or path not been configured","")
		Return False
	End If
End Sub

Sub stanfordButton_MouseClicked (EventData As MouseEvent)
	Dim dc As DirectoryChooser
	dc.Initialize
	stanfordPathTextField.Text=dc.Show(MainForm)
	pathSetting.Put("corenlp",stanfordPathTextField.Text)
	File.WriteMap(File.DirData("BasicCAT"),"serverpath",pathSetting)
End Sub

Sub languageToolButton_MouseClicked (EventData As MouseEvent)
	Dim dc As DirectoryChooser
	dc.Initialize
	languageToolPathTextField.Text=dc.Show(MainForm)
	pathSetting.Put("languagetool",languageToolPathTextField.Text)
	File.WriteMap(File.DirData("BasicCAT"),"serverpath",pathSetting)
End Sub

Sub launchLanguageToolButton_MouseClicked (EventData As MouseEvent)
	If languageToolPathTextField.Text="" Then
		Return
	End If
	If launchLanguageToolButton.Text="Stop LanguageTool Server" Then
		languageToolShell.KillProcess
		launchLanguageToolButton.Text="Start LanguageTool Server"
	Else
		launchLanguageToolServer
		launchLanguageToolButton.Text="Stop LanguageTool Server"
	End If
	
End Sub

Sub launchCorenlpButton_MouseClicked (EventData As MouseEvent)
	If stanfordPathTextField.Text="" Then
		Return
	End If
	If launchCorenlpButton.Text="Stop CoreNLP Server" Then
		corenlpShell.KillProcess
		launchCorenlpButton.Text="Start CoreNLP Server"
	Else
		launchCorenlpServer
		launchCorenlpButton.Text="Stop CoreNLP Server"
	End If
	
End Sub

Sub launchLanguageToolServer
	languageToolShell.Initialize("languageToolShell","java",Array As String("-cp","languagetool-server.jar","org.languagetool.server.HTTPServer","--port","8081"))
	languageToolShell.WorkingDirectory = languageToolPathTextField.Text
	languageToolShell.RunWithOutputEvents(-1)
End Sub

Sub launchCorenlpServer
	corenlpShell.Initialize("corenlpShell","java",Array As String("-mx4g","-cp",$""*""$, "edu.stanford.nlp.pipeline.StanfordCoreNLPServer", "-port", "9000", "-timeout", "15000"))
	corenlpShell.WorkingDirectory = stanfordPathTextField.Text
	corenlpShell.RunWithOutputEvents(-1)
End Sub


